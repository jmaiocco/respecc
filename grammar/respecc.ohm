respecc {
  Program      = br* greeting? br* Statement (br+ Statement)* br* farewell? br*
  Statement    = Declaration
               | Conditional
               | Loop
               | FuncCallStmt
               | Assignment
               | SimpleStmt
  SimpleStmt   = "print" "(" Exp? ")"                            -- print
               | "return" Exp?                                   -- return_impolite
               | "Kindly return" Exp?                            -- return_polite
               | "break"                                         -- break_impolite
               | "You deserve a break!"                          -- break_polite
  Conditional  = "if" "(" Exp ")" Block
                 (br* "else" "if" "(" Exp ")" Block)*
                 (br* "else" Block)?                             -- if_impolite
               | "Excuse me, if " Exp "," Block
                 (br* "Otherwise, if " Exp "," Block)*
                 (br* "Otherwise," Block)?                       -- if_polite
  Loop         = "while" "(" Exp ")" Block                       -- while_impolite
               | "for"  "(" (VarDec)? ";" Exp?
                 ";" Assignment? ")" Block                       -- for_impolite
               | "Excuse me, while " Exp "," Block               -- while_polite
  FuncCallStmt = id Params                                       -- call_impolite
               | "Could you do me a favor and run" id
                 ("with" Params)? "?"                            -- call_polite
  FuncCallExp  = id Params                                       -- call_impolite
               | "the result of running" id
                 ("with" Params)?                                -- call_polite
  Assignment   = Var "=" Exp                                     -- impolite
               | Increment
               | "Please populate " Var "with " Exp "."          -- polite
  Declaration  = VarDec | FuncDec// | TypeDec
  Type         =  Type "[" "]"                                   -- array
               |  primtype
               |  id
  FuncDec       = "Favor" id Params (":" Type)? Block             -- polite
               | "function" id Params (":" Type)? Block          -- impolite
  VarDec       = "Please declare " id
                 ("as a " Type)? ("as " Exp)? "."                -- polite
               | "gimme" id (":" Type)? "=" Exp                  -- impolite
  Params       = "(" ListOf<Param, ","> ")"
  Param        = id (":" Type)?
  Block        =  "could you..." br* (Statement br+)*
                  "Thank You."                                   -- polite
               |  "{" br* (Statement br+)* "}"                   -- impolite
  Exp          =  Exp1 "?" Exp1 ":" Exp                          -- ternary
               |  Exp1
  Exp1         =  Params "->" Block                              -- lambda_blk
               |  Params "->" Exp2                               -- lambda_exp
               |  Exp2
  Exp2         =  Exp2 orop Exp3                                  -- binary
               |  Exp3
  Exp3         =  Exp3 andop Exp4                                -- binary
               |  Exp4
  Exp4         =  Exp5 relop Exp5                                -- binary
               |  Exp5
  Exp5         =  Exp5 addop Exp6                                -- binary
               |  Exp6
  Exp6         =  Exp6 mulop Exp7                                -- binary
               |  Exp7
  Exp7         =  prefixop Exp9                                  -- prefix
               |  Exp8
  Exp8         =  Exp9 expoop Exp8                               -- binary
               |  "-" Exp9                                       -- negate
               |  Exp9
  Exp9         =  Literal
               |  FuncCallExp
               |  Var
               |  "(" Exp ")"                                    -- parens
  Increment    =  incop Var                                      -- prefix
               |  Var incop                                      -- postfix
  Var          =  Var "[" intlit "]"                                -- subscript
               |  Var "." id                                     -- select
               |  id
  Literal      = intlit
               | stringlit
               | boollit
  keyword      =  ("Boolean" | "if" | "break" | "else" | "int"
               |  "for" | "and" | "return" | "Number" | "plus"
               |  "null" | "while" | "true" | "String"
               |  "or" | "Yes" | "No" | "minus" | "times" | "not"
               |  "print" | "Favor" | "function" | "gimme" ) ~idrest
  id           = ~keyword letter idrest*
  idrest       = letter | digit | "_"
  boollit      = "Yes" | "No"
  intlit       = digit+
  stringlit    = "\"" char* "\"" | "\'" char* "\'"
  char         = ~"\\" ~"\"" ~"\n" any | escape
  escape       = "\\" ("\\" | "\"" | "n" | "t" | codepoint)
  codepoint    = "u{" hexDigit+ "}"
  andop        = "&&" | "and"
  orop         = "||" | "or"
  mulop        = "*" | "/" | "%" | "times" | "divided by" | "modded with"
  addop        = "+" | "-" | "plus" | "minus"
  expoop       = "**" | "raised to the power of"
  relop        = "<=" | "<" | ">=" | ">" | "==" | "!="  | "is less than or equal to"
               | "is greater than or equal to" | "is less than" | "is greater than"
               | "is equal to" | "is not equal to"
  incop        =  "++" | "--"
  prefixop     =  ~"--" "-" | "!" | "not"
  primtype     =  "Boolean" | "Number" | "String"
  space        := comment | " " | "\t"
  comment      = "/*" (~"*/" any)* "*/"
  br           =  ";" | "\n"
  greeting     = ("Hello!" | "Hey!" | "Hi There!" | "Salutations!")
  farewell     = ("Bye Bye!" | "Farewell!")
}
